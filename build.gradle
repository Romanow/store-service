plugins {
    id "idea"
    id "java-library"
    id "org.jetbrains.kotlin.jvm" version "1.9.25"
    id "org.jetbrains.kotlin.kapt" version "1.9.25" apply false
    id "org.jetbrains.kotlin.plugin.jpa" version "1.9.25" apply false
    id "org.jetbrains.kotlin.plugin.spring" version "1.9.25" apply false
    id "org.jlleitschuh.gradle.ktlint" version "12.2.0" apply false
    id "org.openapi.generator" version "7.8.0" apply false
    id "org.springframework.boot" version "3.4.5" apply false
    id "com.gorylenko.gradle-git-properties" version "2.4.2" apply false
    id "com.adarshr.test-logger" version "4.0.0" apply false
    id "io.spring.dependency-management" version "1.1.7"
}

ext {
    kotlinVersion = "1.9.25"
    springBootVersion = "3.4.5"
    springGatewayVersion = "4.2.0"
    springCircuitBreakerVersion = "3.2.1"
    validationApiVersion = "2.0.1.Final"
    wireMockVersion = "3.12.1"
    jwtVersion = "4.5.0"
    openApiVersion = "2.8.5"
    logbookVersion = "3.12.0"
    logstashEncoderVersion = "7.4"
    aggregatorByTagsVersion = "1.0.1"
    wireMockVersion = "3.10.0"
    postgresContainerVersion = "1.21.0"
    keycloakContainerVersion = "3.7.0"
}

subprojects { ->
    apply plugin: "idea"
    apply plugin: "kotlin"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "io.spring.dependency-management"

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    [compileKotlin, compileTestKotlin]*.kotlinOptions*.jvmTarget = "17"

    repositories {
        mavenCentral()
    }

    ktlint {
        version.set("1.2.1")
        filter {
            exclude { entry -> entry.file.toString().contains("generated") }
        }
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-gateway-dependencies:$springGatewayVersion"
            mavenBom "org.springframework.cloud:spring-cloud-circuitbreaker-dependencies:$springCircuitBreakerVersion"
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        implementation "org.jetbrains.kotlin:kotlin-reflect"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin"
        implementation "jakarta.validation:jakarta.validation-api"
    }

    processResources {
        filesMatching(["**/properties-from-gradle.properties"]) {
            expand(project.properties)
        }
    }
}
